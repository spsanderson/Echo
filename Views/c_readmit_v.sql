USE [SMS]

-- Create a new view called 'c_readmit_v' in schema 'dbo'
-- Drop the view if it already exists
IF EXISTS (
SELECT *
	FROM sys.views
	JOIN sys.schemas
	ON sys.views.schema_id = sys.schemas.schema_id
	WHERE sys.schemas.name = N'dbo'
	AND sys.views.name = N'c_readmit_v'
)
DROP VIEW dbo.c_readmit_v
GO

-- Create the view in the specified schema
CREATE VIEW dbo.c_readmit_v
AS
-- body of the view
WITH CTE
AS (
	SELECT MRN,
		PT_NO,
		UNIT_NO,
		ADMIT_DATE,
		DSCH_DATE,
		ACCT_TYPE,
		PT_TYPE,
		PT_TYPE_DESC,
		[FILE],
		FC,
		FC_DESCRIPTION,
		HOSP_SVC,
		HOSP_SVC_DESCRIPTION,
		REC_ID = ROW_NUMBER() OVER (
			PARTITION BY MRN ORDER BY ADMIT_DATE ASC
			)
	FROM dbo.Pt_Accounting_Reporting_ALT
	WHERE MRN != ''
		AND Tot_Chgs > 0
	)
SELECT C1.MRN,
	C1.PT_NO AS [INDEX_ACCT],
	C1.UNIT_NO AS [INDEX_UNIT],
	C2.PT_NO AS [READMIT_ACCT],
	C2.UNIT_NO AS [READMIT_UNIT],
	CAST(C1.ADMIT_DATE AS DATE) AS [INDEX_ADMIT_DATE],
	CAST(C1.DSCH_DATE AS DATE) AS [INDEX_DSCH_DATE],
	CAST(C2.ADMIT_DATE AS DATE) AS [READMIT_ADMIT_DATE],
	CAST(C2.DSCH_DATE AS DATE) AS [READMIT_DSCH_DATE],
	C1.ACCT_TYPE AS [INDEX_ACCT_TYPE],
	C1.PT_TYPE AS [INDEX_PT_TYPE],
	C1.PT_TYPE_DESC AS [INDEX_PT_TYPE_DESC],
	C1.[FILE] AS [INDEX_FILE],
	C1.FC AS [INDEX_FC],
	C1.FC_DESCRIPTION AS [INDEX_FC_DESCRIPTION],
	C1.HOSP_SVC AS [INDEX_HOSP_SVC],
	C1.HOSP_SVC_DESCRIPTION AS [INDEX_HOSP_SVC_DESCRIPTION],
	C2.ACCT_TYPE AS [READMIT_ACCT_TYPE],
	C2.PT_TYPE AS [READMIT_PT_TYPE],
	C2.PT_TYPE_DESC AS [READMIT_PT_TYPE_DESC],
	C2.[FILE] AS [READMIT_FILE],
	C2.FC AS [READMIT_FC],
	C2.FC_DESCRIPTION AS [READMIT_FC_DESCRIPTION],
	[INTERVAL] = DATEDIFF(DAY, C1.DSCH_DATE, C2.ADMIT_DATE)
FROM CTE AS C1
INNER JOIN CTE AS C2 ON C1.MRN = C2.MRN
WHERE C1.ADMIT_DATE < C2.ADMIT_DATE
	AND C1.REC_ID + 1 = C2.REC_ID
	AND DATEDIFF(DAY, C1.DSCH_DATE, C2.ADMIT_DATE) > 0
GO


